include(CheckTypeSize)
include(CheckIncludeFileCXX)

set(ARES_PUSH_FLAGS ${CMAKE_REQUIRED_FLAGS})
set(CMAKE_REQUIRED_FLAGS "/std:c++17 -std=c++17")
check_include_file_cxx(memory_resource HAVE_MEMORY_RESOURCE)
if (HAVE_MEMORY_RESOURCE)
  set(CMAKE_EXTRA_INCLUDE_FILES "memory_resource")
  check_type_size("struct std::pmr::pool_options" STRUCT_STD_PMR_POOL_OPTIONS LANGUAGE CXX)
  set(CMAKE_EXTRA_INCLUDE_FILES)
  if (HAVE_STRUCT_STD_PMR_POOL_OPTIONS)
    message("Compiler support for std::pmr seems to be complete")
  else()
    message("Compiler does not support std::pmr::pool_options")
    set(ARES_NO_STD_PMR ON)
  endif()
else()
  message("Include file <memory_resource> is not found")
  set(ARES_NO_STD_PMR ON)
endif()
set(CMAKE_REQUIRED_FLAGS ${ARES_PUSH_FLAGS})

unset(ARES_BOOST_NEEDED_LIBS)
if (ARES_NO_STD_PMR)
  message("Compiler support for std::pmr is incomplete, will fallback to Boost")
  set(ARES_COMPILE_DEFINITIONS ${ARES_COMPILE_DEFINITIONS} "ARES_NO_STD_PMR")
  set(ARES_BOOST_NEEDED_LIBS ${ARES_BOOST_NEEDED_LIBS} container)
endif()

if (ARES_BOOST_NEEDED_LIBS)
  find_package(Boost 1.66.0 REQUIRED ${ARES_BOOST_NEEDED_LIBS})
  if(NOT Boost_FOUND)
    message(FATAL_ERROR "Could not find Boost. Try setting BOOST_INCLUDEDIR and BOOST_LIBRARYDIR. See https://cmake.org/cmake/help/v3.0/module/FindBoost.html for more info.")
  endif()
  message("Boost include dirs ${Boost_INCLUDE_DIRS}")
  set(ARES_INCLUDE_DIRECTORIES ${ARES_INCLUDE_DIRECTORIES} ${Boost_INCLUDE_DIRS})
  set(ARES_LINK_LIBRARIES ${ARES_LINK_LIBRARIES} ${Boost_CONTAINER_LIBRARY})  
endif()
