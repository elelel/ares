# Libpq (dll or MSVC lib) location. Required for MSVC, optional for MinGW and Non-Win
if (ARES_LIBPQ_LIB)
  set(ARES_POSTGRES_LIBS ${ARES_POSTGRES_LIBS} ${ARES_LIBPQ_LIB})
else()
  if (WIN32)
    if (MINGW)
      message("Compiling on Windows with MINGW, but ARES_LIBPQ_LIB is not specified. Relying on package system. If linking to libpq.dll fails, specify it through ARES_LIBPQ_LIB variable explicitly")
      set(NEED_FIND_PACKAGE_LIBPQ_LIB "yes")
    elseif (MSVC)
      message(FATAL_ERROR "Compiling on Windows with MSVC, but ARES_LIBPQ_LIB is not specified. Assuming no package system is available. Specify ARES_LIBPQ_LIB exlicitly (should point to libpq library)")
    endif()
  else()
   # message("Compiling on Non-Windows platform, ARES_LIBPQ_LIB is not specified. Will rely on package system")
    set(NEED_FIND_PACKAGE_LIBPQ_LIB "yes")
  endif()
endif()

# Libpq include headers
if (ARES_LIBPQ_INCLUDE_DIRS)
  set(ARES_POSTGRES_INCLUDE_DIRS ${ARES_POSTGRES_INCLUDE_DIRS} ${ARES_LIBPQ_INCLUDE_DIRS})
else()
  if (WIN32)
    if (MINGW)
      message("Compiling on Windows with MINGW, but ARES_LIBPQ_INCLUDE_DIRS is not specified. Relying on package system. If including libpq fails, specify it through ARES_LIBPQ_INCLUDE_DIRS variable explicitly")
      set(NEED_FIND_PACKAGE_LIBPQ_INCLUDE_DIRS "yes")
    elseif (MSVC)
      message(FATAL_ERROR "Compiling on Windows with MSVC, but ARES_LIBPQ_INCLUDE_DIRS is not specified. Assuming no package system is available. Specify ARES_LIBPQ_INCLUDE_DIRS exlicitly")
    endif()
  else()
    # message("Compiling on Non-Windows platform, ARES_LIBPQ_INCLUDE_DIRS is not specified. Will rely on package system")
    set(NEED_FIND_PACKAGE_LIBPQ_INCLUDE_DIRS "yes")
  endif()
endif()

# Invoke FindPackage PostgreSQL if needed
if (NEED_FIND_PACKAGE_LIBPQ_LIB OR NEED_FIND_PACKAGE_LIBPQ_INCLUDE_DIRS)
  find_package(PostgreSQL REQUIRED)
  if (NEED_FIND_PACKAGE_LIBPQ_LIB)
    set(ARES_POSTGRES_LIBS ${ARES_POSTGRES_LIBS} ${PostgreSQL_LIBRARIES})
  endif()
  if (NEED_FIND_PACKAGE_LIBPQ_INCLUDE_DIRS)
    set(ARES_POSTGRES_INCLUDE_DIRS ${ARES_POSTGRES_INCLUDE_DIRS} ${PostgreSQL_INCLUDE_DIRS})
  endif()
endif()
